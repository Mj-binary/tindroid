apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

// See explanation here: https://stackoverflow.com/questions/20562189/sign-apk-without-putting-keystore-info-in-build-gradle
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'co.tinode.tindroid'

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    defaultConfig {
        applicationId "co.tinode.tindroidx"
        minSdkVersion 27
        targetSdkVersion 35
        compileSdk 35
        vectorDrawables.useSupportLibrary = true

        // If you are getting errors due to git not being setup comment out lines gitVersion*()
        // and uncomment lines with static assignments.

        buildConfigField 'int', 'VERSION_CODE', "${rootProject.ext.version_code}"
        buildConfigField 'String', 'VERSION_NAME', "\"${rootProject.ext.version_name}\""

        versionCode rootProject.ext.version_code
        versionName rootProject.ext.version_name
    }

    buildTypes {
        debug {
            resValue "string", "default_host_name", '"sandbox.tinode.co"'
        }
        release {
            resValue "string", "default_host_name", '"api.tinode.co"'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs.
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/libs']
        }
    }

    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':tinodesdk')

    // Camera libraries must use the the same version.
    def camerax_version = '1.4.2'
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"

    implementation 'androidx.activity:activity:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.3.0'
    implementation 'androidx.core:core-splashscreen:1.0.1'

    // noinspection GradleDependency: 1.4 requires API 34
    implementation 'androidx.emoji2:emoji2:1.3.0'
    implementation 'androidx.exifinterface:exifinterface:1.4.1'
    implementation 'androidx.fragment:fragment:1.8.6'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.2.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'androidx.work:work-runtime:2.10.1'

    implementation 'com.android.installreferrer:installreferrer:2.2'

    implementation 'androidx.media3:media3-exoplayer:1.6.1'
    implementation 'androidx.media3:media3-ui:1.6.1'
    implementation 'androidx.media3:media3-datasource:1.6.1'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.1.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'com.google.firebase:firebase-crashlytics:19.4.3'
    implementation 'com.google.firebase:firebase-messaging:24.1.1'
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'

    implementation 'com.googlecode.libphonenumber:libphonenumber:9.0.2'

    // WebRTC.
    implementation 'com.dafruits:webrtc:123.0.0'
    // Image loading and caching.
    implementation 'io.coil-kt:coil:2.7.0'
    // QR Code generator.
    implementation 'io.nayuki:qrcodegen:1.8.0'

    implementation 'com.squareup.okhttp3:okhttp-tls:4.12.0'

    annotationProcessor 'androidx.lifecycle:lifecycle-common-java8:2.9.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}

configurations {
    configureEach {
        exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
        resolutionStrategy {
            force 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22'
        }
    }
}